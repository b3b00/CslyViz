
genericLexer MinimalXmlLexer;

[Mode]
[Push("tag")]
[Sugar] OPEN : "<";

[UpTo] CONTENT : "<"; 

[Mode]
[Push("pi")]
[Sugar] OPEN_PI : "<?";

[Push("comment")]
[Sugar] OPEN_COMMENT : "<!--";

[Mode("comment")]
[UpTo] COMMENT_CONTENT : "-->"; 

[Mode("comment")]
[Pop]
[Sugar] CLOSE_COMMENT : "-->";

[Mode("tag")]
[Mode("pi")]
[AlphaId] ID;

[Mode("tag")]
[Sugar] SLASH : "/";

[Mode("tag", "pi")]
[Sugar] EQUALS : "=";

[Mode("tag", "pi")]
[Mode("pi")]
[String] VALUE;

[Mode("pi")]
[Pop]
[Sugar] CLOSE_PI : "?>";

[Mode("tag")]
[Pop]
[Sugar] CLOSE : ">";



parser MinimalXmlParser;

@subNodes(leaders,null,trailers);
-> document : [comment | processing-instruction | content]* element [comment | processing-instruction | content]*;

@node(selfClosingTag);
element : OPEN[d] ID attribute* SLASH[d] CLOSE[d];

opentag : OPEN[d] ID attribute* CLOSE[d];

closetag : OPEN[d] SLASH[d] ID CLOSE[d];

@subNodes(null, elements, null);
@node(tag);
element : opentag [element|processing-instruction|comment|content]* closetag;

misc : [comment | processing-instruction | content];

comment : OPEN_COMMENT[d] COMMENT_CONTENT CLOSE_COMMENT[d];

@node(processing);
processing-instruction : OPEN_PI[d] ID attribute* CLOSE_PI[d];

attribute: ID EQUALS[d] VALUE;

content : CONTENT;

